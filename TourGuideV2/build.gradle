// TourGuideV2

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:4.6.0"
  }
}

plugins {
	id 'org.springframework.boot' version '2.4.0'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'jacoco'
	id 'checkstyle'
	id "com.github.spotbugs" version "4.6.0"
}

apply plugin: "com.github.spotbugs"
apply plugin: 'io.spring.dependency-management'

group = 'com.trip-master'
version = '2.1'
sourceCompatibility = '11'

repositories {
	mavenCentral()
	jcenter()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.projectreactor:reactor-spring:1.0.1.RELEASE'
    
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    implementation group: 'org.javamoney', name: 'moneta', version: '1.3'
    implementation group: 'com.jsoniter', name: 'jsoniter', version: '0.9.23'
    
    implementation group: 'com.puppycrawl.tools', name: 'checkstyle', version: '8.37'
	
	testImplementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.0'
	testImplementation group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '4.9.0'
	
	implementation "io.springfox:springfox-boot-starter:3.0.0"
}

// BOOT JAR ********************************************************************
bootJar {
    manifest {
        attributes 'Start-Class': 'com.tripmaster.TourGuideV2.TourGuideV2Application'
    }
    
}

// TESTS ***********************************************************************
test {
    useJUnitPlatform()
    
    String testType = System.properties['test.type']
    if (testType == 'integration') {
        include '**/*IntegrationTest.*'
        include '**/*IntegrationSpec.*'
    } else if (testType == 'unit') {
        include '**/*Test.*'
        include '**/*Spec.*'
        exclude '**/*IntegrationTest.*'
        exclude '**/*IntegrationSpec.*'
    } else if (testType == 'all') {
        include '**/*Test.*'
        include '**/*Spec.*'
    } else {
        //Default to unit
        include '**/*Test.*'
        include '**/*Spec.*'
        exclude '**/*IntegrationTest.*'
        exclude '**/*IntegrationSpec.*'
    }
    
    jacoco {
        enabled = true
        destinationFile = file("$buildDir/jacoco/${name}.exec")
        includes = []
        excludes = []
        excludeClassLoaders = []
        includeNoLocationClasses = false
        sessionId = "<auto-generated value>"
        dumpOnExit = true
        classDumpDir = null
        output = JacocoTaskExtension.Output.FILE
        address = "localhost"
        port = 6300
        jmx = false
    }
}


// JACOCO **********************************************************************
jacoco {
    toolVersion = "0.8.4"
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      limit {
        counter = 'LINE'
        value = 'COVEREDRATIO'
        minimum = 0.5
      }
    }
  }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/libs/**']
            )
        }))
    }
}
test.finalizedBy jacocoTestReport
check.dependsOn jacocoTestCoverageVerification

// CHECKSTYLE *************************************************
checkstyle {
    configFile = file('build/reports/checkstyle-checker.xml');
    reportsDir = file("$project.buildDir/reports/checkstyle/")
}

checkstyleTest.enabled = false

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

// SPOTBUGS****************************************************
spotbugsMain {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
spotbugsTest {
    reports {
        xml.enabled = false
        html.enabled = false
    }
}